#!/usr/bin/perl

# Copyright (C) 2010 Tamil s.a.r.l.
#
# This file is part of Koha.
#
# Koha is free software; you can redistribute it and/or modify it
# under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
#
# Koha is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with Koha; if not, see <http://www.gnu.org/licenses>.

package Main;

use FindBin;
use lib $FindBin::Bin;

use strict;
use warnings;

use LangInstaller;
use Getopt::Long;
use Pod::Usage;

use Koha::Caches;


my $verbose     = 0;
my $pref        = 0;
my $all         = 0;
my @files;
GetOptions(
    'v|verbose' => \$verbose,
    'p'         => \$pref,
    'f:s'       => \@files,
    'a|all'     => \$all,
);


sub usage {
    pod2usage( -verbose => 2 );
    exit;
}


usage() if $#ARGV != 1 && $#ARGV != 0;

my ($cmd, $lang) = @ARGV;
$cmd = lc $cmd;
if ( $cmd =~ /^(install|compress|uncompress)$/ ) {
    my $installer = LangInstaller->new(
        {
            lang      => $lang,
            pref_only => $pref,
            verbose   => $verbose
        }
    );
    if ( $lang and not grep( {$_ eq $lang} @{ $installer->{langs} } ) ) {
        print "Unsupported language: $lang\n";
        exit;
    }
    if ( $all ) {
        for my $lang ( @{$installer->{langs}} ) {
            $installer->set_lang( $lang );
            $installer->$cmd(\@files);
        }
    }
    else {
        $installer->$cmd(\@files);
    }

    Koha::Caches->get_instance()->flush_all;
} elsif ($cmd eq 'create' or $cmd eq 'update') {
    my $command = "gulp po:$cmd";
    $command .= " --silent" if (!$verbose);
    $command .= " --lang $lang" if ($lang);

    if ($verbose) {
        print STDERR "Deprecation notice: PO creation and update are now gulp tasks. See docs/development/internationalization.md\n";
        print STDERR "Running `$command`\n";
    }

    system($command);
} else {
    usage();
}



=head1 NAME

translate - Handle templates and preferences translation

=head1 SYNOPSYS

  translate install fr-FR
  translate install fr-FR -f search -f memberentry
  translate -p install fr-FR
  translate compress [fr-FR]
  translate uncompress [fr-FR]

=head1 DESCRIPTION

In Koha, three categories of information are translated based on standard GNU
.po files: opac templates pages, intranet templates and system preferences. The
script is a wrapper. It allows to quickly install .po files for a
given language or for all available languages.

=head1 USAGE

Use the -v or --verbose parameter to make translator more verbose.

=over

=item translate [-p|-f] install F<lang>

Use .po files to translate the english version of templates and preferences files
and copy those files in the appropriate directory. Without F<lang>, all
available languages are installed. With -p option, only preferences .po file is
updated.

With -f parameter (repeatable) you can specify specific files to translate. For
example, -f search will translate all templates containing 'search'.

=item translate compress F<lang>

Compress .po files in F<po> directory, named F<lang>-*.po. Without F<lang>, files
from all available languages are compressed.

=item translate uncompress F<lang>

Uncompress .po.gz files in F<po> directory, named F<lang>-*.po.gz. Without F<lang>,
files from all available languages are uncompressed.


=back

=cut

