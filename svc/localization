#!/usr/bin/perl

use Modern::Perl;
use Encode qw( encode );

use C4::Service;
use Koha::Caches;
use Koha::Database;

our ( $query, $response ) = C4::Service->init( parameters => 'parameters_remaining_permissions' );

sub get_translations {
    my $draw = $query->param('draw');
    my $start = $query->param('start');
    my $length = $query->param('length');
    my $search = $query->param('search[value]');

    my $schema = Koha::Database->new->schema;
    my $rs = $schema->resultset('L10nSource');
    my $recordsTotal = $rs->count;

    my $cond = {};
    if ($search) {
        my @and;
        my @words = split /\s+/, $search;
        foreach my $word (@words) {
            push @and, \['source LIKE ? COLLATE utf8mb4_unicode_ci', "%$word%"];
        }
        $cond->{-and} = \@and;
    }

    $rs = $rs->search($cond);
    my $recordsFiltered = $rs->count;

    my @sources = $rs->search({}, {
        prefetch => 'l10n_targets',
        result_class => 'DBIx::Class::ResultClass::HashRefInflator',
        rows => $length,
        offset => $start,
        order_by => { -asc => ['context', 'source'] },
    });

    $response->param(
        draw => $draw,
        recordsTotal => $recordsTotal,
        recordsFiltered => $recordsFiltered,
        data => \@sources,
    );
    C4::Service->return_success( $response );
}

sub update_translation {
    my $id = $query->param('id');
    my $translation = $query->param('translation');
    my $lang = $query->param('lang');

    my $schema = Koha::Database->new->schema;
    my $rs = $schema->resultset('L10nTarget');

    if (defined $translation && $translation ne '') {
        $rs->update_or_create({
            l10n_source_id => $id,
            language => $lang,
            translation => $translation,
        }, {
            key => 'l10n_source_language',
        });
    } else {
        $rs->search({
            l10n_source_id => $id,
            language => $lang,
        })->delete;
    }

    my $source = $schema->resultset('L10nSource')->find($id);
    my $cache_key = sprintf('%s:%s', $source->context, $lang);
    Koha::Caches->get_instance()->clear_from_cache($cache_key);

    C4::Service->return_success( $response );
}

C4::Service->dispatch(
    [ 'GET /', [], \&get_translations ],
    [ 'PUT /', [ 'id' ], \&update_translation ],
);
